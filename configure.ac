dnl#                                               -*- Autoconf -*-
# configure.ac for dlcompat
# Process with autoconf to generate a configure script
#
# This originally only did minimal checks, but that was all that was
# needed. Now it does more checks.

AC_PREREQ([2.69])
AC_INIT([dlcompat],[20030629],[ogorman@users.sourceforge.net])
AC_CONFIG_SRCDIR([dlfcn.c])
VERSION=20030629
AC_SUBST([VERSION])
OPT=-O2

# Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

# Checks for libraries.
dnl# If you already have dlopen in your libdl, dlcompat will probably be
dnl# unnecessary for you, but whatever...
AC_CHECK_LIB([dl],[dlopen])

# Checks for header files.
AC_CHECK_HEADERS([limits.h])
AC_CHECK_HEADER([mach-o/dyld.h],[],
                [AC_MSG_ERROR([No dyld.h found, cannot continue.])])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_INLINE
AC_C_PROTOTYPES
AC_C_RESTRICT
AC_SYS_LARGEFILE
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_ERROR_AT_LINE
AC_FUNC_MALLOC
AC_CHECK_FUNCS([atexit strchr])
AC_CHECK_FUNC([NSLinkModule],[],
              [AC_MSG_ERROR([NSLinkModule not found, cannot continue.])])
AC_CHECK_FUNC([NSAddImage],[],
              [AC_MSG_WARN([Not tested without NSAddImage, good luck!])])

# debug option
AC_ARG_ENABLE([debug],
              [AS_HELP_STRING([--enable-debug],[build a debug version])],[
		OPT="-O0 -g"
		DEBUGDEF="-DDEBUG=2"
		],[])

# for the fink package manager, build a version that is binary compatible
# with all previous releases of dlcompat
# no automatic underscore prepending
# lib is named libdl.0.dylib
AC_ARG_ENABLE([fink],
              [AS_HELP_STRING([--enable-fink],[build a fink version])],[
		FINKDEF="-DFINK_BUILD=1"
		VERSTRING=""
		LIBVER=0
		NOTPREPEND="not"
		],[
		FINKDEF=""
		VERSTRING="-compatibility_version 1 -current_version 1"
		LIBVER=1
		NOTPREPEND="indeed"
		])

AC_SUBST([OPT])
AC_SUBST([DEBUGDEF])
AC_SUBST([FINKDEF])
AC_SUBST([VERSTRING])
AC_SUBST([LIBVER])
AC_SUBST([NOTPREPEND])
AC_CONFIG_FILES([./Makefile
		])
dnl# do NOT put dlopen.3 here; it needs to be be generated by the Makefile,
dnl# and NOT by the configure script
AC_OUTPUT
